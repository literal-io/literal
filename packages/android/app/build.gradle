apply plugin: 'com.android.application'
apply plugin: 'com.amazonaws.appsync'

def signingPropertiesFile = rootProject.file('./signing.properties')
def signingProperties = new Properties()
signingProperties.load(new FileInputStream(signingPropertiesFile))

android {
    signingConfigs {
        beta {
            storeFile file(signingProperties['BETA_STORE_FILE'])
            storePassword signingProperties['BETA_STORE_PASSWORD']
            keyAlias signingProperties['BETA_KEY_ALIAS']
            keyPassword signingProperties['BETA_KEY_PASSWORD']
        }
        release {
            storeFile file(signingProperties['RELEASE_STORE_FILE'])
            storePassword signingProperties['RELEASE_STORE_PASSWORD']
            keyAlias signingProperties['RELEASE_STORE_ALIAS']
            keyPassword signingProperties['RELEASE_KEY_PASSWORD']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "io.literal"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 8
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        beta {
            initWith release
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            signingConfig signingConfigs.beta
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }
    }

}

task buildSourceWebViewScripts(type: Exec) {
  commandLine rootProject.file('./source-webview-scripts/build.sh')
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease' || task.name == 'assembleDebug') {
        task.dependsOn buildSourceWebViewScripts
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.activity:activity:1.2.0-rc01"
    implementation "androidx.fragment:fragment:1.3.0-rc02"
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0-rc01'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation "commons-io:commons-io:2.6"
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    implementation 'io.sentry:sentry-android:4.3.0'

    //
    // AWS
    //
    // Base SDK
    implementation 'com.amazonaws:aws-android-sdk-core:2.19.3'
    // AppSync SDK
    implementation 'com.amazonaws:aws-android-sdk-appsync:3.1.2'
    // S3 SDK
    implementation 'com.amazonaws:aws-android-sdk-s3:2.19.3'
    implementation('com.amazonaws:aws-android-sdk-auth-userpools:2.19.3@aar') { transitive = true }
    // Auth
    implementation 'com.amazonaws:aws-android-sdk-mobile-client:2.19.3'
    implementation 'com.amazonaws:aws-android-sdk-cognitoauth:2.19.3'
    implementation('com.amazonaws:aws-android-sdk-auth-google:2.19.3@aar') { transitive = true }
}
